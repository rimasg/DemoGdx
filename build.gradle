import com.badlogic.gdx.tools.texturepacker.TexturePacker

buildscript {
    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.1.4'
        classpath 'com.badlogicgames.gdx:gdx-tools:1.9.8'
    }
}

allprojects {
    apply plugin: "idea"

    version = '1.0'
    ext {
        appName = "DemoGdx"
        gdxVersion = '1.9.8'
        roboVMVersion = '1.12.0'
        box2DLightsVersion = '1.4'
        ashleyVersion = '1.7.3'
        aiVersion = '1.8.1'
        gdxUtilsVersion = '0.13.4'
    }

    repositories {
        google()
        jcenter()
        mavenCentral()
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        maven { url "https://oss.sonatype.org/content/repositories/releases/" }
    }
}

project(":desktop") {
    apply plugin: "java"

    dependencies {
        implementation project(":core")
        implementation "com.badlogicgames.gdx:gdx-backend-lwjgl:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-desktop"
        implementation "com.badlogicgames.gdx:gdx-tools:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-desktop"
    }
}

project(":android") {
    apply plugin: "android"

    configurations { natives }

    dependencies {
        implementation project(":core")
        implementation fileTree(dir: '../libs', include: ['*.jar'])
        implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"
        implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86_64"
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
        natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"
        implementation "com.badlogicgames.ashley:ashley:$ashleyVersion"
        implementation "com.badlogicgames.gdx:gdx-ai:$aiVersion"
    }
}

project(":core") {
    apply plugin: "java"

    dependencies {
        implementation fileTree(dir: '../libs', include: ['*.jar'])
        implementation "com.badlogicgames.gdx:gdx:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
        implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
        implementation "com.badlogicgames.ashley:ashley:$ashleyVersion"
        implementation "com.badlogicgames.gdx:gdx-ai:$aiVersion"
        implementation "net.dermetfan.libgdx-utils:libgdx-utils:$gdxUtilsVersion"
        implementation "net.dermetfan.libgdx-utils:libgdx-utils-box2d:$gdxUtilsVersion"
    }

    task packGamePlayTextures {
        def inputDir = "$rootDir/desktop/assets-raw/gameplay"
        def outputDir = "$rootDir/android/assets/gameplay"

        def atlasFile = 'gameplay'
        def pack = new File("${outputDir}/${atlasFile}.png")
        def atlas = new File("${outputDir}/${atlasFile}.atlas")

        inputs.dir inputDir
        outputs.file pack
        outputs.file atlas

        doLast {
            pack.delete()
            atlas.delete()
            TexturePacker.process(inputDir, outputDir, "${atlasFile}");
        }
    }

    task packUiTextures {
        def inputDir = "$rootDir/desktop/assets-raw/ui"
        def outputDir = "$rootDir/android/assets/ui"

        def atlasFile = 'skin'
        def pack = new File("${outputDir}/${atlasFile}.png")
        def atlas = new File("${outputDir}/${atlasFile}.atlas")

        inputs.dir inputDir
        outputs.file pack
        outputs.file atlas

        doLast {
            pack.delete()
            atlas.delete()
            TexturePacker.process(inputDir, outputDir, "${atlasFile}");
        }
    }

    project.tasks.compileJava.dependsOn packGamePlayTextures
    project.tasks.compileJava.dependsOn packUiTextures
}
